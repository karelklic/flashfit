; flashfit.nsis - Installer script for Windows.
; In Fedora, install mingw32-nsis package and then
; makensis flashfit.nsis

!include "MUI2.nsh"
!include "Sections.nsh"
!include "LogicLib.nsh"
!include "WordFunc.nsh"

; The name of the installer
Name "FlashFit ${PACKAGE_VERSION}"
Caption "FlashFit ${PACKAGE_VERSION}"

; The file to write
OutFile "flashfit-${PACKAGE_VERSION}.exe"

;Interface Settings
!define MUI_ABORTWARNING

; The default installation directory
InstallDir $PROGRAMFILES\FlashFit-${PACKAGE_VERSION}

; Registry key to check for directory (so if you install again, it will 
; overwrite the old one automatically)
InstallDirRegKey HKLM "Software\FlashFit${PACKAGE_VERSION}" "Install_Dir"

SetCompressor zlib

; Necessary for correct Vista uninstallation.
RequestExecutionLevel admin

;===============================================================================
; Pages
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
;!define MUI_FINISHPAGE_RUN "$INSTDIR\flashfit.pyw"
!define MUI_FINISHPAGE_NOREBOOTSUPPORT
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"

;===============================================================================
; The stuff to install
;
Section "FlashFit (required)"
  SectionIn 1 2 3 RO
  SetOutPath $INSTDIR

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR
  
  SetOverwrite on
  File "*.py"
  File /oname=flashfit.pyw "flashfit.py"
  File "AUTHORS"
  File "COPYING"
  File "NEWS"
  File "README"
  
  ; Write the installation path into the registry
  WriteRegStr HKLM "SOFTWARE\FlashFit${PACKAGE_VERSION}" "Install_Dir" "$INSTDIR"
  
  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\FlashFit${PACKAGE_VERSION}" "DisplayName" "FlashFit ${PACKAGE_VERSION}"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\FlashFit${PACKAGE_VERSION}" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegExpandStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\FlashFit${PACKAGE_VERSION}" "InstallLocation" "$INSTDIR"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\FlashFit$${PACKAGE_VERSION}" "DisplayVersion" "${PACKAGE_VERSION}"
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\FlashFit${PACKAGE_VERSION}" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\FlashFit${PACKAGE_VERSION}" "NoRepair" 1
  WriteUninstaller "uninstall.exe"
SectionEnd

;===============================================================================
; Optional section (can be disabled by the user)
;
Section "Start Menu Shortcuts"
  SetShellVarContext all
  CreateDirectory "$SMPROGRAMS\FlashFit ${PACKAGE_VERSION}"
  CreateShortCut  "$SMPROGRAMS\FlashFit ${PACKAGE_VERSION}\Uninstall.lnk" "$INSTDIR\uninstall.exe"
  CreateShortCut  "$SMPROGRAMS\FlashFit ${PACKAGE_VERSION}\FlashFit.lnk" "$INSTDIR\flashfit.pyw"
SectionEnd

;===============================================================================
; Uninstaller
;
Section Uninstall
  ; Remove registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\FlashFit${PACKAGE_VERSION}"
  DeleteRegKey HKLM "Software\FlashFit\FlashFit${PACKAGE_VERSION}"

  ; Remove files and uninstaller
  Delete "$INSTDIR\*.py"
  Delete "$INSTDIR\*.pyw"
  Delete "$INSTDIR\*.pyc"
  Delete "$INSTDIR\AUTHORS"
  Delete "$INSTDIR\COPYING"
  Delete "$INSTDIR\NEWS"
  Delete "$INSTDIR\README"
  Delete "$INSTDIR\uninstall.exe"

  ; Remove shortcuts, if any
  SetShellVarContext all
  Delete "$SMPROGRAMS\FlashFit ${PACKAGE_VERSION}\*.*"
  RMDir "$SMPROGRAMS\FlashFit ${PACKAGE_VERSION}"

  ; Remove directories used
  RMDir $INSTDIR
SectionEnd
